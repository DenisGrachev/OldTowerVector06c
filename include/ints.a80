

;palUpdateFlag equ $+1
;        ld a,0

;interrupt proc
ints:	
	push hl : push bc : push de : push af
	
        ld a,88h;Установить режимы портов: 02 - вывод, 03 - вывод
        out (00),a;мл.4 бита порта 01 - вывод, старшие 4 бита - ввод

        ;check if need update pal
palUpdateFlag equ $+1
        ld a,1
        or a : jp z,skipUpdatePal
        dec a : ld (palUpdateFlag),a;don't load pal on next frame      
palPointer:		ld hl,COLR15;в пару "HL" - адрес последнего цвета таблицы цветов	
        ld d,16;в "D" - счетчик колич. устанавл. цветов (размер табл)
        ld e,15;в "E" - адрес для записи в ОЗУ цветогенератора
INIT1:  ld a,e;;установить адрес ОЗУ цвета через
        out (02),a;порт 02
        ld a,(hl);взять в "A" байт (физический код) цвета из таблицы цветов
        out (0Ch),a;записать этот байт в ОЗУ цветогенератора и повторить
        out (0Ch),a;несколько раз для надежной записи по одному адресу,
        out (0Ch),a;т.к. в некоторых "Векорах" наблюдается очень плохая
        out (0Ch),a;запись в ОЗУ цветогенератора и лучше подстраховаться.
        out (0Ch),a;
        dec hl;уменьшить адрес-указатель в таблице цветов
        out (0Ch),a
        dec e;уменьшить адрес для ОЗУ цветогенератора
        out (0Ch),a;
        dec d;уменьшить счетчик байт в таблице цветов
        out (0Ch),a
        jp nz,INIT1;если не вся таблица записана в цв.ген. - продолжать
skipUpdatePal:

        ld a,8Ah;установить режимы портов 02-ввод, 03-вывод
        out (0),a;мл. 4 бита порта 01 - вывод, старшие 4 бита - ввод
        
        ld a,11111110b;установить в младшем разряде порта 03 нулевой потенциал
        out (03),a;чтобы опросить 1-ую линейку клавиатуры (курсоры и т.д.)
        in a,(02);взять из порта 02 физич. код нажатия клавиш линейки
        ld (KeyKod0),a;поместить этот код в ячейку "KeyKod"

        ;линейка где пробел
        ld a,01111111b
        out (03),a
        in a,(02)
        ld (KeyKod7),a


        ld a,88h;установить режимы портов 02-вывод, 03-вывод
        out (00),a;мл. 4 бита порта 01 - вывод, старшие 4 бита - ввод
        ld a,(Border);взять цвет бордюра ячейки "Border" (матем. код) и
        out (02),a;установить цвет бордюра на 4-х младших битах порта 02
        ld a,(Scroll);взять из ячейки "Scroll"вертикальное положение экрана
        out (03),a;и установить в порт 03
        ;ld a,(IndRus);взять признак включения индикатора "РУС/LAT"
        ;ld b,a;временно поместить в регистр "B"
        ;ld a,(Rele);взять признак включения реле из ячейки "Rele" и
        ;or b;об'единить с признаком включения индикатора "РУС/LAT"
        ;out (01),a;вывести сигналы управления индикатором и реле в порт 01        
        call doJoy
        call doFade
        IF MUTE_MUSIC=0
	call vktPlay
        ENDIF
        call doSounds	
	
	pop af : pop de : pop bc : pop hl
	ei
    ret